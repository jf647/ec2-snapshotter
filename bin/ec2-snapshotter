#!/usr/bin/perl
#

use strict;
use warnings;

use FindBin;
use DateTime::Format::RFC3339;
use DateTime::Format::Duration;
use Getopt::Long;
use Log::Log4perl;
use LWP::UserAgent;
use Net::Amazon::EC2;
use YAML 'LoadFile';

my %o = (
    configfile => "$FindBin::Bin/../etc/ec2-snapshotter.yaml",
    verbose => 0,
    debug => 0,
    ec2debug => 0,
    trace => 0,
    force => 0,
);
GetOptions(
    \%o,
    "configfile=s",
    "verbose!",
    "debug!",
    "ec2debug!",
    "trace!",
    "force!",
) or die "can't parse command line!";
die "can't parse command line!" if( @ARGV );

# load our config
my $cfg = LoadFile( $o{configfile} )
    or die "can't load config $o{configfile}!";

# init logging
my $logcfg = $cfg->{logging};
my $loglevel = 'INFO';
my $logchannel = 'System';
if( $o{debug} ) {
    $loglevel = 'DEBUG';
}
if( $o{trace} ) {
    $loglevel = 'TRACE';
}
if( $o{verbose} ) {
    $logchannel = 'System, Screen';
}
$logcfg->{logger} = "$loglevel, $logchannel";
Log::Log4perl->init( $logcfg );
my $log = Log::Log4perl->get_logger;
$log->info('START');

# create an EC2 connection
$log->debug("creating EC2 connection");
my $ec2 = Net::Amazon::EC2->new(
    AWSAccessKeyId => $cfg->{auth}->{accesskey},
    SecretAccessKey => $cfg->{auth}->{secretkey},
    region => $cfg->{awsregion},
    debug => $o{ec2debug},
) or $log->logdie("can't instantiate EC2 interface!");
$log->trace("created EC2 connection");

# create a web agent
my $ua = LWP::UserAgent->new;

# create a time parser
my $now = DateTime->now;
my $f = DateTime::Format::RFC3339->new;
my $d = DateTime::Format::Duration->new(
    pattern => '%Y years, %m months, %e days, %H hours, %M minutes',
);

# create a SNS connection if required
my $sns;
my $topic;
my @notify;
if( exists $cfg->{sns_notify} ) {
    eval "require Amazon::SNS";
    if( $@ ) {
        $log->logdie("unable to load Amazon::SNS: $@");
    }
    $log->debug("creating SNS connection");
    $sns = Amazon::SNS->new( {
        key => $cfg->{auth}->{accesskey},
        secret => $cfg->{auth}->{secretkey},
        service => "http://sns.$cfg->{awsregion}.amazonaws.com",
    } ) or $log->logdie("can't instantiate SNS interface!");
    $log->trace("created SNS connection");
    $log->debug("getting handle on SNS topic $cfg->{sns_notify}");
    $topic = $sns->GetTopic( $cfg->{sns_notify} )
        or $log->logdie("can't get handle on topic '$cfg->{sns_notify}'!");
    $log->trace("got handle on SNS topic $cfg->{sns_notify}");
}

# get a list of all our snapshots
my $snaps = get_snaps();

# get the list of volume IDs to operate on
my @volids;
if( 'config' eq $cfg->{'volumes-source'} ) {
    @volids = @{ $cfg->{volumes} };
}
else {
    $log->logdie("volume source method '$cfg->{'volumes-source'}' is not supported!");
}

# for each volume, make sure we've made a snapshot within the required timeframe
my $refresh_snaps = 0;
for my $volid( @volids ) {

    $log->info("processing volume $volid");

    # get our snapshot schedule
    my $snapcfg = $cfg->{snapshot_schedule}->{$volid} || $cfg->{snapshot_schedule}->{'*'}
        || $log->logdie("can't get snapshot schedule for volume id '$volid'!");

    # validate the volume
    $log->debug("validating volume $volid");
    my $vol = $ec2->describe_volumes( VolumeId => $volid );
    if( 'Net::Amazon::EC2::Errors' eq ref $vol ) {
        error( describe_volumes => $vol );
    }
    $log->trace("validated volume $volid");

    # find the newest snapshot
    my $newest_snaptime;
    my $newest_snap;
    my $found = 0;
    for my $snap( @$snaps ) {
        next unless( $snap->volume_id eq $volid );
        $found = 1;
        next if( 'error' eq $snap->status );
        $log->debug("considering snapshot ", $snap->snapshot_id);
        my $snaptime = $f->parse_datetime( $snap->start_time );
        $log->debug("snapshot is in ", $snap->status, " status and was started at ", $snaptime);
        if( ! defined $newest_snaptime || $snaptime > $newest_snaptime ) {
            $newest_snaptime = $snaptime;
            $newest_snap = $snap->snapshot_id;
            $log->trace("this is now the newest snapshot");
        }
    }
    
    
    # if we found a snap, is it too old?
    if( $found ) {
        my $maxage = $now - DateTime::Duration->new( $snapcfg );
        $log->debug("newest snapshot is dated $newest_snaptime");
        $log->debug("oldest allowed snapshot date is $maxage");
        if( $newest_snaptime < $maxage ) {
            $log->info("newest snapshot is too old - creating a new one");
            my $newsnap = $ec2->create_snapshot( VolumeId => $volid, Description => 'ec2-snapshotter' );
            $log->info("created snapshot ", $newsnap->snapshot_id);
            $refresh_snaps = 1;
            if( $topic ) {
                my $date = scalar localtime( time );
                push @notify, "created snapshot " . $newsnap->snapshot_id . " of '$volid' at $date";
            }        
        }
    }
    
    # if we didn't find a snap, we need to create one
    else {
        $log->info("no snapshot available - creating a new one");
        my $newsnap = $ec2->create_snapshot( VolumeId => $volid, Description => 'ec2-snapshotter' );
        $log->info("created snapshot ", $newsnap->snapshot_id);
        $refresh_snaps = 1;
        if( $topic ) {
            my $date = scalar localtime( time );
            push @notify, "created snapshot " . $newsnap->snapshot_id . " of '$volid' at $date";
        }
        next;
    }

}

if( $refresh_snaps ) {
    $snaps = get_snaps();
}

# build a list of volumes and their snapshots, sorted by starttime
my $vol_to_snap;
for my $snap( @$snaps ) {
    my $snapinfo = {
        snapshot_id => $snap->snapshot_id,
        volume_id => $snap->volume_id,
        start_time => $f->parse_datetime( $snap->start_time ),
    };
    push @{ $vol_to_snap->{$snap->volume_id} }, $snapinfo;
}
for my $volid( keys %$vol_to_snap ) {
    @{ $vol_to_snap->{$volid} } = sort { $a->{start_time} <=> $b->{start_time} } @{ $vol_to_snap->{$volid} };
}

# walk through the volumes, purging for each snapshot threshold
my @to_del;
for my $volid( @volids ) {
    my $purgecfg = $cfg->{purge_schedule}->{$volid} || $cfg->{purge_schedule}->{'*'}
        || $log->logdie("can't get purge schedule for volume id '$volid'!");
    $log->debug("purging for volume $volid");
    my $newest = $vol_to_snap->{$volid}->[$#{ $vol_to_snap->{$volid} }]->{start_time};
    my $newest_id = $vol_to_snap->{$volid}->[$#{ $vol_to_snap->{$volid} }]->{snapshot_id};
    $log->debug("newest snapshot is $newest_id, dated ", $newest);
    my $start_weeks_after = $purgecfg->{hours} + ( $purgecfg->{days} * 24 );
    my $start_months_after = $start_weeks_after + ( $purgecfg->{weeks} * 24 * 7 );
    my $prev_start_date = DateTime->from_epoch( epoch => 0, time_zone => 'local' )->strftime('%Y-%m-%d');
    my $start_date;
    for my $snapinfo( @{ $vol_to_snap->{$volid} } ) {
        my $snap_date = $snapinfo->{start_time};
        $log->debug("considering snapshot $snapinfo->{snapshot_id} dated $snap_date");
        my $snap_age_h = int( ( $now->epoch - $snap_date->epoch ) / (60*60) );
        my $snap_age_d = int( ( $now->epoch - $snap_date->epoch ) / (60*60*24) );
        $log->trace("snapshot age in hours is $snap_age_h; in days $snap_age_d");
        my $type;
        my $keep_count = 0;
        if( $snap_age_h > $purgecfg->{hours} ) {
            if( $snap_age_h <= $start_weeks_after ) {
                $type = 'days';
                $start_date = $snap_date->clone->set_time_zone( 'local' )->strftime('%Y-%m-%d');
            }
            else {
                if( $snap_age_h <= $start_months_after ) {
                    $type = 'weeks';
                    my $week_day = $snap_date->strftime('%w');
                    my $epoch = $snap_date->epoch - ($week_day * (60*60*24));
                    my $dt = DateTime->from_epoch( epoch => $epoch );
                    $start_date = DateTime->from_epoch( epoch => $epoch, time_zone => 'local' )->strftime('%Y-%m-%d');
                }
                else {
                    $type = 'months';
                    my $dt = $snap_date->clone->set_time_zone( 'local' )->truncate( to => 'month' );
                    $start_date = $dt->strftime('%Y-%m-%d');
                }
            }
            if( $start_date ne $prev_start_date ) {
                $prev_start_date = $start_date;
                $log->debug("keeping snapshot $snapinfo->{snapshot_id}; $snap_age_d days old; keeping for the $type of $start_date");
                $keep_count++;
            }
            else {
                if( $snapinfo->{snapshot_id} eq $newest_id ) {
                    $log->debug("keeping snapshot $snapinfo->{snapshot_id}; will not delete newest snapshot");
                    $keep_count++;
                }
                else {
                    $log->info("deleting snapshot $snapinfo->{snapshot_id}; $snap_age_d days old");
                    push @to_del, $snapinfo->{snapshot_id};
                }
            }
        }
        else {
            $log->debug("keeping snapshot $snapinfo->{snapshot_id}; $snap_age_h hours old (min hours $purgecfg->{hours})");
            $keep_count++;
        }
    }
        
}

if( @to_del ) {
    for my $snapid( @to_del ) {
        $log->debug("deleting snapshot $snapid");
        my $rc = $ec2->delete_snapshot( SnapshotId => $snapid );
        if( 'Net::Amazon::EC2::Errors' eq ref $rc ) {
            error( delete_snapshot => $rc );
        }
        if( $topic ) {
            my $date = scalar localtime( time );
            push @notify, "deleted snapshot $snapid at $date";
        }
    }
}

# send a SNS notification if needed
if( @notify ) {
    $log->debug("publishing to SNS topic");
    $topic->Publish( join("\n", @notify) );
    $log->trace("published to SNS topic");
}

# all done
$log->info('STOP');
exit(0);

# dump EC2 errors
sub error
{

    my $method = shift;
    my $errors = shift;

    for my $error( @{ $errors->errors } ) {
        $log->error("\t", $error->code, ": ", $error->message);
    }
    $log->logdie("can't call $method!");

}

sub get_snaps
{

    $log->debug("getting snapshot info");
    my $snaps = $ec2->describe_snapshots( Owner => 'self' );
    if( 'Net::Amazon::EC2::Errors' eq ref $snaps ) {
        error( describe_snapshots => $snaps );
    }
    return $snaps;

}

#
# EOF
